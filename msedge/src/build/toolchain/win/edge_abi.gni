# Copyright (C) Microsoft Corporation. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# Runs the script to generate ABI compatible headers to consume Winmd files.
#
# Parameters:
#   input:
#     Path to the .winmd file.
#
#   output_dir:
#     Output directory for the generated header files.
#
#   outputs:
#     The names of the files that will be generated in the output directory
#
import("//build/config/win/visual_studio_version.gni")
assert(is_win)

template("edge_abi") {
  action(target_name) {
    input_file = invoker.input
    output_dir = invoker.output_dir
    outputs = invoker.outputs
    metadata_dir = "$windows_sdk_path/UnionMetadata"
    tool_path = "//buildtools/third_party/abi_winrt/bin/abi.exe"
    prebuilt_dir = rebase_path("//third_party/win_build_output") + "/winmd/" +
                   rebase_path(output_dir, root_gen_dir)

    sources = [ input_file ]

    script = "//build/toolchain/win/edge_generate_abi_headers.py"

    if (current_cpu == "x86") {
      win_tool_arch = "environment.x86"
    } else if (current_cpu == "x64") {
      win_tool_arch = "environment.x64"
    } else if (current_cpu == "arm64") {
      win_tool_arch = "environment.arm64"
    } else {
      assert(false, "Need environment for this arch")
    }

    args = [
      "--abi_path",
      rebase_path(tool_path),
      "--input_file",
      rebase_path(input_file),
      "--output_dir",
      rebase_path(output_dir),
      "--metadata_dir",
      rebase_path(metadata_dir),
      "--source_dir",
      rebase_path(prebuilt_dir),
      "--architecture",
      win_tool_arch,
    ]
    foreach(output, outputs) {
      args += [
        "--output",
        output,
      ]
    }

    outputs = []
    foreach(output, invoker.outputs) {
      outputs += [ "$output_dir/$output" ]
    }
  }
}
